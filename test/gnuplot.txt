#!/usr/bin/gnuplot

#set terminal png
set terminal postscript eps enhanced color solid size 11cm,7cm "Helvetica" 14
#set terminal pdf color solid size 11cm,7cm font "Helvetica,10"

if (!exists("file")) file='coin10.txt'

set datafile separator ","

set yrange [* : *]
set ytics nomirror
set y2tics
set link y2 via 10**((48.13 - y)/20) inverse 48.13 - 20*log10(y)

set autoscale xfixmin
set autoscale xfixmax
set autoscale yfixmin
set autoscale yfixmax

set offsets 50, 50, 0.5, 0.5
set format x "%.0fKB"
set ylabel "PSNR" offset 1.5,0,0
set y2label "RMSE" offset -1.5,0,0

set grid ytics lt 0 lw 1 lc rgb "#bbbbbb"
set grid xtics lt 0 lw 1 lc rgb "#bbbbbb"

set style line 1 lc rgb '#0060ad' lt 1 lw 2 pt 7 pi -1 ps 1.0
set style line 2 lc rgb '#ad0000' lt 1 lw 2 pt 7 pi -1 ps 1.0
set style line 3 lc rgb '#00a060' lt 1 lw 2 pt 7 pi -1 ps 1.0
set style line 4 lc rgb '#ad9800' lt 1 lw 2 pt 7 pi -1 ps 1.0

set key inside bottom left
set key height 1
set key width 1


set pointintervalbox 3

range(t,x)=(t eq "mrgb" ? x:NaN)
classic(t,x)=(t eq "lptm"? ($7/1000): (t eq "ptm"? ($7/1000):(t eq "yhsh"? ($7/1000):(t eq "hsh"? ($7/1000):NaN))))

plot file \
   using ((strcol(3) eq "mrgb")? ($7/1000):NaN):8 with linespoint ls 3 title "RBF", \
'' using ((strcol(2) eq "bilinear")? ($7/1000):NaN):8 with linespoint ls 4 title "Bilinear", \
'' using ((strcol(3) eq "mycc")? ($7/1000):NaN):8 with point ls 1 title "YCCxyy", \
'' using (classic(strcol(1), ($7/1000))):8 with point ls 2 notitle, \
\
'' using ((strcol(3) eq "mrgb")? ($7/1000):NaN):8:4 with labels left offset 0.7,-0.5 notitle, \
'' using ((strcol(2) eq "bilinear")? ($7/1000):NaN):8:4 with labels left offset 0.7,-0.5 notitle, \
'' using ((strcol(3) eq "mycc")? ($7/1000):NaN):8:(sprintf("%d.%d", $6, ($4-$6)/2)) with labels left offset 0.7,-0.5 notitle, \
'' using (classic(strcol(1), ($7/1000))):8:1 with labels left offset 0.7,-0.5 notitle

#pause -1 "Hit any key to continue"

